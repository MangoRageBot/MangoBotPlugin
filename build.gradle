import org.mangorage.mangobotgradle.util.GitVersion

buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
        maven {
            url = 'https://repo.mattmalec.com/repository/releases'
        }
        maven {
            url = "https://maven.mangorage.org/releases/"
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
        classpath 'org.mangorage:MangoBotGradle:6.0.22'
    }
}

plugins {
    id 'org.gradlex.extra-java-module-info' version '1.12'
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'
apply plugin: 'MangoBotGradle'
apply plugin: 'maven-publish'

group = 'org.mangorage'
version = GitVersion.getGitVersion().getVersionAsString()

println("Version: " + version)

MangoBotConfig {
    setJarTask(jar)
    addRunConfig {
        it.setName("runDevBot")
        it.addArgs("--launchTarget", "mangobot")
        it.addArgs("--dev")
    }
}

extraJavaModuleInfo {
    setDeriveAutomaticModuleNamesFromFileNames(true)
}

repositories {
    mavenCentral()
    mavenLocal()

    maven {
        url = "https://maven.mangorage.org/releases/"
    }
    maven {
        url = 'https://maven.minecraftforge.net/'
    }
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
    maven {
        url "https://maven.lavalink.dev/releases/"
    }
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    installer('org.mangorage:installer:4.0.26')
    bootstrap("org.mangorage:mangobotbootstrap:1.0.84")
    launchtarget("org.mangorage:mangobotlaunchtarget:0.1.8")

    plugin('org.mangorage:mangobot:12.0.101')

    library('org.slf4j:slf4j-simple:2.0.13') // Use a recent version)
    library('org.luaj:luaj-jme:3.0.1')

    library('dev.arbjerg:lavaplayer:+')
    library('dev.lavalink.youtube:common:+')

    library("com.google.zxing:core:3.5.4")
    library("com.google.zxing:javase:3.5.4")
}

test {
    useJUnitPlatform()
}

tasks.named('processResources', ProcessResources).configure {
    var replaceProperties = [
            mangobot_version: version
    ]
    inputs.properties replaceProperties

    filesMatching(['mangobot.plugin.json']) {
        expand replaceProperties + [project: project]
    }
}

publishing {
    publications.register("mangobotaddon", MavenPublication) {
        artifact jar

        pom {
            name = 'MangoBotPlugin'
            description = 'The Main Plugin For MangoBot'
            url = 'https://github.com/MangoRageBot/MangoBotPlugin'
            issueManagement {
                system = 'github'
                url = 'https://github.com/MangoRageBot/MangoBotPlugin/issues'
            }
            developers {
                developer {
                    id = 'mangorage'
                    name = 'MangoRage'
                }
            }
            withXml {
                def dependencies = asNode().appendNode('dependencies')
                configurations.library.getDependencies().each {
                    def dependency = dependencies.appendNode('dependency')
                    dependency.appendNode('groupId', it.group)
                    dependency.appendNode('artifactId', it.name)
                    dependency.appendNode('version', it.version)
                }
            }
        }
    }

    repositories {
        maven {
            url = "https://maven.mangorage.org/releases/"

            credentials {
                username = System.getenv("MAVEN_USER")
                password = System.getenv("MAVEN_PASSWORD")
            }
        }
    }
}